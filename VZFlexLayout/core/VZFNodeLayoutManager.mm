//
//  VZFNodeLayoutManager.m
//  O2OReact
//
//  Created by moxin on 16/3/28.
//  Copyright Â© 2016å¹´ Alipay. All rights reserved.
//

#import "VZFNodeLayoutManager.h"
#import "VZFUtils.h"
#import "VZFNodeLayout.h"
#import "VZFNodeInternal.h"
#import "VZFNodeLifeCycle.h"
#import "VZFNodeMountContext.h"
#import "VZFMacros.h"
#import <stack>
#import "VZFAsyncDrawingTransactionContainer.h"
#import "VZFRasterizeNodeTool.h"

using namespace VZ::UIKit;

namespace VZ {
    
    MountResult mountInContext(VZFNode *node, const VZ::UIKit::MountContext &context, CGSize size, VZFNode* parentNode, BOOL asyncDisplay, VZFRasterizeCachePolicy rasterizeCachePolicy, BOOL cannotBeRasterized, BOOL isUpdating) {
        if (asyncDisplay) {
            return [node renderInContext:context
                                    Size:size
                              ParentNode:parentNode
                      cannotBeRasterized:cannotBeRasterized
                    rasterizeCachePolicy:rasterizeCachePolicy];
        } else {
            return [node mountInContext:context
                                   Size:size
                             ParentNode:parentNode
                             isUpdating:isUpdating];
        }
        
    }
    
    NSSet<VZFNode*>* layoutRootNodeInContainer(NodeLayout layout, UIView* container, NSSet<VZFNode* >* previousNodes, VZFNode* superNode) {
        return layoutRootNodeInContainer(layout, container, previousNodes, superNode, NO, NO);
    }
    
    NSSet<VZFNode*>* layoutRootNodeInContainer(NodeLayout layout, UIView* container, NSSet<VZFNode* >* previousNodes, VZFNode* superNode, BOOL rasterizeUseCache, BOOL isUpdating){
        //This method should be called on main thread
        VZFCAssertMainThread();

        VZFC_LOG_THREAD(@"LayoutManager",@"LayoutRootNode");
        
        //è®¾ç½®æ ¹èŠ‚ç‚¹layer ä½œä¸ºå¼‚æ­¥æ¸²æŸ“çš„ç®¡ç†èŠ‚ç‚¹
        container.layer.isAsyncTransactionContainer = YES;
        
        NSSet<VZFNode *> *clipAndCannotBeRasterizedNodes = nil;
        
        if (layout.node && layout.node.specs.asyncDisplay) {
            clipAndCannotBeRasterizedNodes = [VZFRasterizeNodeTool getClipAndCannotBeRasterizedNodes:layout];
        }
        
        //0, è®¡ç®—å‡ºRoot Nodeçš„layout
        struct MountItem{
            const NodeLayout& layout;
            MountContext context;
            VZFNode* superNode;
            BOOL isVisited;
        };
        
        //1, ç»‘å®šroot hosting view
        //    layout.node.rootNodeView = container;
        
        //ä¿å­˜mountå‡ºæ¥çš„nodes
        NSMutableSet* mountedNodes = [NSMutableSet set];

        // ç¡®ä¿ unmount å‰ rootContext è¢«é‡Šæ”¾ï¼Œå¦åˆ™æœ‰äº› node çš„ on-update-disappear äº‹ä»¶ä¸ä¼šå›è°ƒ
        {
            //2.1, åˆ›å»ºrootContext
            MountContext rootContext = MountContext::RootContext(container);
            rootContext.position = layout.origin;

            //2.2, åˆ›å»ºä¸€ä¸ªstackç”¨æ¥é€’å½’
            std::stack<MountItem> stack = {};

            stack.push({layout,rootContext,superNode,NO});

            static NSDictionary *defaultAutoAnimationAttributes = @{
                                                             VZFAutoAnimationEnabled: @NO,
                                                             VZFAutoAnimationDuration: @0.2,
                                                             };

            if (layout.node.specs.autoAnimationAttributes) {
                NSMutableDictionary *newDict = defaultAutoAnimationAttributes.mutableCopy;
                [newDict addEntriesFromDictionary:layout.node.specs.autoAnimationAttributes];
                layout.node.specs.autoAnimationAttributes = newDict;
            }
            else {
                layout.node.specs.autoAnimationAttributes = defaultAutoAnimationAttributes;
            }

            //2.3, æ¯ä¸ªèŠ‚ç‚¹æ·±åº¦ä¼˜å…ˆéå†
            /**
             * @discussion:
             * è¿™é‡Œä»æ ¹èŠ‚ç‚¹å¼€å§‹éå†æ¯ä¸€ä¸ªå­èŠ‚ç‚¹ï¼Œä¼šäº§ç”Ÿé¢‘ç¹çš„é€’å½’è°ƒç”¨
             * å¯ä»¥ä½¿ç”¨lambdaè¡¨è¾¾å¼åšå‡½æ•°å¼é€’å½’ï¼Œä½†è€ƒè™‘è¿™ä¸ªè¿‡ç¨‹å¾ˆé¢‘ç¹ï¼Œè¿™é‡Œä½¿ç”¨while+stackçš„é€’å½’æ–¹å¼ï¼Œå‡å°‘æ ˆç©ºé—´çš„é¢‘ç¹å¼€é”€
             */
            while (!stack.empty()) {

                //è¿™é‡Œé¢å–å¼•ç”¨ï¼Œå› ä¸ºè¦æ”¹å˜å®ƒçš„çŠ¶æ€
                MountItem& item = stack.top();
                if(item.isVisited){

                    //@discussion:æ‰€æœ‰child mountå®Œå†é€šçŸ¥
                    [item.layout.node didMount];
                    stack.pop();

                }
                else{

                    //åˆ›å»ºä¸€ä¸ªmark
                    item.isVisited = YES;

                    if(item.layout.node == nil){
                        continue;
                    }

                    //will mount
                    [item.layout.node willMount];


                    VZFNode *node = item.layout.node;

                    BOOL asyncDisplay = node.specs.asyncDisplay;
                    BOOL cannotBeRasterized = [clipAndCannotBeRasterizedNodes containsObject:node];

                    VZFRasterizeCachePolicy cachePolicy = VZFRasterizeCachePolicyNode;

                    if (asyncDisplay && rasterizeUseCache) {
                        cachePolicy |= VZFRasterizeCachePolicyLayer;
                    }

                    //åŠ è½½nodeï¼Œåˆ›å»ºbacking view
                    //è¿™ä¸ªæ–¹æ³•å¿…é¡»åœ¨ä¸»çº¿ç¨‹è°ƒç”¨
                    MountResult mountResult = mountInContext(node, item.context, item.layout.size, item.superNode, asyncDisplay, cachePolicy, cannotBeRasterized, isUpdating);

                    [mountedNodes addObject:item.layout.node];

                    //VZFNSLog(@"<Mounted:%@ -> %@>",item.layout.node.class,item.layout.node.superNode.class);

                    if (mountResult.hasChildren) {

                        /**
                         *  @discussionï¼šæ³¨æ„ä¸¤ç‚¹:
                         *
                         *  1, ç†è®ºä¸Šä½¿ç”¨FlexNode layoutå‡ºæ¥çš„ nodeğŸŒ²å’Œåº”è¯¥å’ŒFNodeğŸŒ²ä¸¥æ ¼ä¸€ä¸€å¯¹åº”
                         *
                         *  2, ä½¿ç”¨åå‘è¿­ä»£å™¨ï¼Œä¿è¯æœ€åº•éƒ¨çš„FNodeå…ˆè¢«mount
                         */

                        for(auto reverseItor = item.layout.children->rbegin(); reverseItor != item.layout.children->rend(); reverseItor ++){
                            // éšå¼åŠ¨ç”»å±æ€§
                            if (reverseItor->node.specs.autoAnimationAttributes) {
                                NSMutableDictionary *newDict = item.layout.node.specs.autoAnimationAttributes.mutableCopy;
                                [newDict addEntriesFromDictionary:reverseItor->node.specs.autoAnimationAttributes];
                                reverseItor->node.specs.autoAnimationAttributes = newDict;
                            }
                            else {
                                reverseItor->node.specs.autoAnimationAttributes = item.layout.node.specs.autoAnimationAttributes;
                            }

                            stack.push(
                                       {*reverseItor,
    //                                       mountResult.childContext.parentOffset((*reverseItor).origin, item.layout.size),
                                           mountResult.childContext.rootOffset((*reverseItor).origin, item.layout.size),
                                           item.layout.node,
                                           NO
                                       });
                        }
                    }

                }

            }
        }
        
        //3, unmountåŸæ¥çš„nodes
        if(previousNodes){
            
            NSMutableSet* nodesToUnmount = [previousNodes mutableCopy];
            [nodesToUnmount minusSet:mountedNodes];
            unmountNodes(nodesToUnmount);
        }
        
        return  mountedNodes;
    }
    

    UIView* viewForRootNode(NodeLayout layout, CGSize sz){
      
        VZFCAssertMainThread();
        
        UIView* container = [[UIView alloc]initWithFrame:CGRect{{0,0},sz}];
        
        layoutRootNodeInContainer(layout, container, nil, nil);
        
        return container;
    }
    
    void unmountNodes(NSSet<VZFNode* >* nodes){
    
        VZFC_LOG_THREAD(@"LayoutManager",@"unmountNodes");
        
        //ä¼šæœ‰åœ¨éä¸»çº¿ç¨‹unmountçš„æƒ…å†µ,bounceåˆ°Main Thread
        if (![NSThread isMainThread]) {
            VZFDispatchMain(0, ^{
                for(VZFNode* node in nodes){
                    [node unmount];
                }
            });
        }
        else {
            for(VZFNode* node in nodes){
                [node unmount];
            }
        }
    }
    
}
