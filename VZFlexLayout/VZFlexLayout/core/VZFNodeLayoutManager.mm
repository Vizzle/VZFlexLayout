//
//  VZFNodeLayoutManager.m
//  O2OReact
//
//  Created by moxin on 16/3/28.
//  Copyright Â© 2016å¹´ Alipay. All rights reserved.
//

#import "VZFNodeLayoutManager.h"
#import "VZFUtils.h"
#import "VZFNodeLayout.h"
#import "VZFNodeInternal.h"
#import "VZFNodeMountContext.h"
#import <stack>

using namespace VZ;
using namespace VZ::UIKit;
@implementation VZFNodeLayoutManager{

    dispatch_queue_t _layoutSerialQueue;
}

+ (instancetype)sharedInstance{

    static VZFNodeLayoutManager* instance = nil;
    static dispatch_once_t onceToken;
    dispatch_once(&onceToken, ^{
        instance = [VZFNodeLayoutManager new];
    });
    return instance;
}


static const char* g_layoutQueueId = "com.React.layout.queue";
- (instancetype)init{
    
    self = [super init];

    if (self) {
        _layoutSerialQueue = dispatch_queue_create(g_layoutQueueId, DISPATCH_QUEUE_SERIAL);
    }
    return self;
}

- (NSSet<VZFNode* >* )layoutRootNode:(const NodeLayout& )layout InContainer:(UIView* )container WithPreviousNodes:(NSSet<VZFNode* >* )previousNodes AndSuperNode:(VZFNode* )superNode{

    //0, è®¡ç®—å‡ºRoot Nodeçš„layout
//    VZFNodeLayout layout = [rootNode computeLayoutThatFits:sz];
    
    struct MountItem{
        const NodeLayout& layout;
        MountContext context;
        VZFNode* superNode; 
        BOOL isVisited;
    };
    
    
    //1, ç»‘å®šroot hosting view
    layout.node.rootNodeView = container;
    
    //ä¿å­˜mountå‡ºæ¥çš„nodes
    NSMutableSet* mountedNodes = [NSMutableSet set];
    
    //2.1, åˆ›å»ºrootContext
    MountContext rootContext = MountContext::RootContext(container);
    //rootContext.position = layout.origin;

    //2.2, åˆ›å»ºä¸€ä¸ªstackç”¨æ¥é€’å½’
    std::stack<MountItem> stack = {};
    stack.push({layout,rootContext,superNode,NO});
  
    //2.3, æ¯ä¸ªèŠ‚ç‚¹æ·±åº¦ä¼˜å…ˆéå†
    while (!stack.empty()) {
        
        //è¿™é‡Œé¢å–å¼•ç”¨ï¼Œå› ä¸ºè¦æ”¹å˜å®ƒçš„çŠ¶æ€
        MountItem& item = stack.top();
        if(item.isVisited){
           
            //@discussion:æ‰€æœ‰child mountå®Œå†é€šçŸ¥
            [item.layout.node didMount];
            stack.pop();
            
        }
        else{
            
            //åˆ›å»ºä¸€ä¸ªmark
            item.isVisited = YES;
            

            if(item.layout.node == nil){
                continue;
            }
            
            //will mount
            [item.layout.node willMount];
            
            //åŠ è½½nodeï¼Œåˆ›å»ºbacking view
            MountResult mountResult = [item.layout.node mountInContext:item.context
                                                                  Size:item.layout.size
                                                            ParentNode:item.superNode];
            [mountedNodes addObject:item.layout.node];
            
            NSLog(@"<Mounted:%@ -> %@>",item.layout.node.class,item.layout.node.superNode.class);
        
            if (mountResult.hasChildren) {
                
                /**
                 *  @discussionï¼šæ³¨æ„ä¸¤ç‚¹:
                 *
                 *  1, ç†è®ºä¸Šä½¿ç”¨FlexNode layoutå‡ºæ¥çš„ nodeğŸŒ²å’Œåº”è¯¥å’ŒFNodeğŸŒ²ä¸¥æ ¼ä¸€ä¸€å¯¹åº”
                 *
                 *  2, ä½¿ç”¨åå‘è¿­ä»£å™¨ï¼Œä¿è¯æœ€åº•éƒ¨çš„FNodeå…ˆè¢«mount
                 */
                
                for(auto reverseItor = item.layout.children->rbegin(); reverseItor != item.layout.children->rend(); reverseItor ++){
                    
                    stack.push(
                        {*reverseItor,
                        mountResult.childContext.parentOffset((*reverseItor).origin, item.layout.size),
                        item.layout.node,
                        NO});
                }
            }
            
        }

    }

    //3, unmountåŸæ¥çš„nodes
    if(previousNodes){
    
        NSMutableSet* nodesToUnmount = [previousNodes mutableCopy];
        [nodesToUnmount minusSet:mountedNodes];
        [self unmountNodes:nodesToUnmount];
    }

    return  mountedNodes;

}

- (void)unmountNodes:(NSSet<VZFNode* >* )nodes{

    for(VZFNode* node in nodes){
        [node unmount];
    }
}

@end
